plugins {
    id "com.github.johnrengelman.shadow"
    id "java"
    id "signing"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "net.creationreborn"
archivesBaseName = "crapi-plugin"
version = version

configurations {
    compileJava.dependsOn(":common:build")
    jar.enabled = false
    jar.dependsOn("shadowJar")
}

repositories {
    mavenCentral()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        name = "spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        name = "velocity"
        url = "https://repo.velocitypowered.com/snapshots"
    }
}

dependencies {
    shadow(project(":common")) {
        exclude(module: "gson")
    }
    annotationProcessor("com.velocitypowered:velocity-api:1.0.8")
    compile("com.velocitypowered:velocity-api:1.0.8")
    compile("net.md-5:bungeecord-api:1.15-SNAPSHOT")
    compile("org.apache.logging.log4j:log4j-slf4j-impl:2.13.3")
    annotationProcessor("org.spongepowered:spongeapi:7.2.0")
    compile("org.spongepowered:spongeapi:7.2.0")
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include("bungee.yml")
        expand("name": "CRAPI", "version": version)
    }
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.shadow]
    relocate("kotlin", "net.creationreborn.api.plugin.lib.kotlin")
    relocate("okhttp3", "net.creationreborn.api.plugin.lib.okhttp3")
    relocate("okio", "net.creationreborn.api.plugin.lib.okio")
    relocate("org.intellij.lang.annotations", "net.creationreborn.api.plugin.lib.lang.annotations")
    relocate("org.jetbrains.annotations", "net.creationreborn.api.plugin.lib.annotations")
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}